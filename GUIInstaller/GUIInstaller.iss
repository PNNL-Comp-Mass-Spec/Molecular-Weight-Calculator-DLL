; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define ReleaseVersion GetFileVersion('..\MolecularWeightCalculatorGUI\bin\Release\MolecularWeightCalculatorGUI.exe')
#define MyAppName "Molecular Weight Calculator"      
#define MyAppExeName "MolecularWeightCalculator.exe"
#define MyAppVersion ReleaseVersion
#define MySource "..\MolecularWeightCalculatorGUI"
#define MyAppPublisher "PNNL"
#define MyAppURL "https://github.com/PNNL-Comp-Mass-Spec/Molecular-Weight-Calculator"
#define InstallerFolder "..\GUIInstaller\Output"
#if ReleaseVersion == ""
     #error MolecularWeightCalculator.dll was not found in the Release directory; rebuild the app in Release mode
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{C2605B62-5E5F-432B-A625-11B65BD00AF1}}
; As AnyCPU, we can install as 32-bit or 64-bit, so allow installing on 32-bit Windows, but make sure it installs as 64-bit on 64-bit Windows
ArchitecturesAllowed=x64 x86
ArchitecturesInstallIn64BitMode=x64
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\MolecularWeightCalculator
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir={#InstallerFolder}
OutputBaseFilename=MolecularWeightCalcGUIInstaller
AppCopyright=© PNNL
VersionInfoVersion={#MyAppVersion}
VersionInfoCompany=PNNL
VersionInfoDescription=MolecularWeightCalculator .NET GUI
VersionInfoCopyright=PNNL
SourceDir={#MySource}
Compression=lzma
SolidCompression=yes
DisableWelcomePage=no
WizardStyle=modern   
; Don't install to all users, instead install to the current user only. May install to all users if running as administrator 
PrivilegesRequired=lowest    
; Allow overriding the install mode via dialog or commandline (hides dialog)
; Command-line flags: /ALLUSERS, /CURRENTUSER
PrivilegesRequiredOverridesAllowed=dialog

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl" 
   
[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "bin\Release\MolecularWeightCalculatorGUI.exe";             DestDir: "{app}"; DestName: "{#MyAppExeName}"; Flags: ignoreversion
Source: "bin\Release\*.dll";                                        DestDir: "{app}"; Flags: ignoreversion
Source: "MolecularWeightCalculator.ico";                            DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "..\Readme.md"; DestName: "Readme.txt";                     DestDir: "{app}"; Flags: ignoreversion isreadme
Source: "..\RevisionHistory.txt";                                   DestDir: "{app}"; Flags: ignoreversion
    
[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; WorkingDir: "{app}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\IconImage.ico"; WorkingDir: "{app}"; Tasks: desktopicon

[Code]
function InitializeSetup(): Boolean;
var
    key: string;
    NetFrameWorkInstalled : Boolean;
    release, minVersion: cardinal;
begin
    // https://docs.microsoft.com/en-us/dotnet/framework/migration-guide/how-to-determine-which-versions-are-installed
    // http://kynosarges.org/DotNetVersion.html
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\v4\Full';
    minVersion := 461808;
    Result := false;
    NetFrameWorkInstalled := RegKeyExists(HKLM, key);
    if NetFrameWorkInstalled =true then
    begin
        Result := RegQueryDWordValue(HKLM, key, 'Release', release);
        Result := Result and (release >= minVersion);
    end;

    if Result =false then
    begin
        MsgBox('This setup requires the .NET Framework version 4.7.2. Please install the .NET Framework and run this setup again.',
            mbInformation, MB_OK);
    end;
end;
